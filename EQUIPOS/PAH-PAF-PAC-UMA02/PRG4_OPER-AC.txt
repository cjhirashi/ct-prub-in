REM PRG4_OPER-AC
REM PROGRAMA DE OPERACION DE PLANTA DE AGUA CALIENTE
REM AUTHOR CARLOS JIMENEZ HIRASHI
REM VERSION 1.0.0

REM DECLARACION DE VARIABLES

    LOCALS SYS 							: REM (LECTURA) VARIABLES DE ACTIVACION DEL SISTEMA
    LOCALS ST_B, PID1, PID2				: REM (LECTURA) ESTADO DE BOMBA Y CONTROL DE DEMANDA
    LOCALS SS_B, SS_BL, CN_BP1, CN_BP2 	: REM (ESCRITURA) VARIABLES DE CONTROL
    LOCALS BL, TM_ON, TM_OFF, BP_OFF 	: REM (VAR INT) VARIABLES DEL SISTEMA

REM VARIABLES DE ENTRADA

    SYS = BV7		: REM ACTIVACION DEL SISTEMA DE CONTROL (0)INACTIVO, (1)ACTIVO
    ST_B = BI26		: REM ESTADO DE OPERACION DE BOMBA DE AGUA

    TM_ON = 60		: REM TIEMPO MINIMO DE OPERACION
    TM_OFF = 10		: REM TIEMPO MINIMO DE PARO

    PID1 = LOOP3	: REM DEMANDA PARA CONTROL DE BYPASS1
    PID2 = LOOP4	: REM DEMANDA PARA CONTROL DE BYPASS2
    BP_OFF = 50		: REM POSICION DE BYPASS 1 Y 2 CON EL SISTEMA EN PARO

REM CONTROL DE SISTEMA

    REM *ARRANQUE Y PARO DE SISTEMA
    	IF SYS = 1 AND TIMEOFF( BL ) > TM_OFF THEN BL = 1
    	IF SYS = 0 AND TIMEON( BL ) > TM_ON THEN BL = 0

    	IF BL = 1 THEN
    		SS_B = 1
    		IF TIMEON(ST_B) > 20 THEN SS_BL = 1 ELSE SS_BL = 0
    	 ELSE
    		SS_BL = 0
    		IF TIMEOFF(SS_BL) > 120 THEN SS_B = 0
    	ENDIF

    REM *ACTIVACION DE CONTROL DE BYPASS1 Y 2
    	IF TIMEON(ST_B) > 30 THEN CN_BP1 = PID1 , CN_BP2 = PID2 ELSE CN_BP1 = BP_OFF , CN_BP2 = BP_OFF

REM VARIABLES DE SALIDA

    BO17 = SS_BL
    BO18 = SS_B
    AO19 = CN_BP1
    AO20 = CN_BP2

END