REM PRG7_DEM (CONTROL: VIGAS FRIAS)
REM GENERACION DE DEMANDA PARA CONTROL DE CALEFACCION Y ENFRIAMIENTO
REM AUTHOR CARLOS JIMENEZ HIRASHI @cjhirashi
REM VERSION 1.0.0

REM DECLARACION DE VARIABLES

    LOCALS OCC

REM VARIABLES DE ENTRADA

    OCC = BV1

REM CONTROL DE TEMPERATURA DE VIGA FRIA

    REM *CONTROL PI SISTEMA OF MAURICIO
        LOCALS ENTRADA_1, SETPOINT_1, ERROR_1, SERROR_1
        LOCALS BP_C_1, RP_C_1, VI_C_1, RI_C_1, DEM_C_1
        LOCALS BP_H_1, RP_H_1, VI_H_1, RI_H_1, DEM_H_1

        REM **DECLARACION DE VARIABLES
	        ENTRADA_1 = AV52
	        SETPOINT_1 = AV53
	        BP_C_1 = 10
	        VI_C_1 = 0.5
	        BP_H_1 = 10
	        VI_H_1 = 0.5

        REM **ALGORITMO DE CONTROL
            REM ***DETECCION DE ERROR DEL SISTEMA
	            ERROR_1 = ENTRADA_1 - SETPOINT_1

            REM ***INCREMENTO DE ERROR EN EL SISTEMA
	            IF OCC = 1 THEN
		            SERROR_1 = SERROR_1 + ( ERROR_1 / ( SCANS * 4 ))
	             ELSE
	            	SERROR_1 = 0
	            ENDIF
            	SERROR_1 = MAX((( 100 / VI_H_1 ) * -1 ) , MIN(( 100 / VI_C_1 ) , SERROR_1 ) )

            REM ***INCREMENTO DE ERROR EN EL SISTEMA
	            IF SERROR_1 > 0 THEN
		            RP_C_1 = ( ERROR_1 / BP_C_1 ) * 100
		            RP_H_1 = 0
		            RI_C_1 = VI_C_1 * SERROR_1
		            RI_H_1 = 0
	             ELSE
		            RP_C_1 = 0
		            RP_H_1 = (( ERROR_1 * -1 ) / BP_H_1 ) * 100
		            RI_C_1 = 0
		            RI_H_1 = VI_H_1 * ( SERROR_1 * -1 )
	            ENDIF

            REM ***ACTIVACION DE DEMANDA DEL SISTEMA
	            IF OCC = 1 THEN
		            DEM_C_1 = MAX( 0 , MIN( 100 , ( RP_C_1 + RI_C_1 )))
		            DEM_H_1 = MAX( 0 , MIN( 100 , ( RP_H_1 + RI_H_1 )))
	             ELSE
		            DEM_C_1 = 0
		            DEM_H_1 = 0
	            ENDIF

        REM **ASIGNACION DE DEMANDA
            AV54 = DEM_C_1
            AV55 = DEM_H_1

END