REM PRG6_COOL_HEAT (CONTROL: VIGAS FRIAS)
REM ACTIVACION DE PLANTAS DE AGUA POR DEMANDA
REM AUTHOR CARLOS JIMENEZ HIRASHI @cjhirashi
REM VERSION 1.0.0

REM DECLARACION DE VARIABLES

    LOCALS CCV1, CCV2, CCV3, CCV4 	: REM (LECTURA) VALVULAS ON/OFF AGUA FRIA
    LOCALS CCV5, CCV6, CCV7, CCV8	: REM (LECTURA) VALVULAS PROP AGUA FRIA
    LOCALS CCV9, CCV10, CCV11		: REM (LECTURA) VALVULAS PROP AGUA FRIA
    LOCALS HCV1, HCV2, HCV3, HCV4	: REM (LECTURA) VALVULAS ON/OFF AGUA CALIENTE
    LOCALS HCV5, HCV6, HCV7, HCV8	: REM (LECTURA) VALVULAS PROP AGUA CALIENTE
    LOCALS HCV9, HCV10, HCV11		: REM (LECTURA) VALVULAS PROP AGUA CALIENTE
    LOCALS UMA1				 		: REM (LECTURA) ESTADO DE OPERACION DE UMA1
    LOCALS CCVB, CCVA, CCVAB, HCVB, HCVA, HCVAB 	: REM (VAR INT) ACTIVACION DE SISTEMAS
    LOCALS CCV, HCV, CV, HV			: REM (ESCRITURA) ACTIVACION DE SISTEMAS

REM VARIABLES DE ENTRADA

    UMA1 = BV1		: REM ACTIVACION DEL SISTEMA POR UMA1
    CCV1 = BO1		: REM VALVULA DE AGUA FRIA ON/OFF
    HCV1 = BO2		: REM VALVULA DE AGUA CALIENTE ON/OFF
    CCV2 = BO3		: REM VALVULA DE AGUA FRIA ON/OFF
    HCV2 = BO4		: REM VALVULA DE AGUA CALIENTE ON/OFF
    CCV3 = BO5		: REM VALVULA DE AGUA FRIA ON/OFF
    HCV3 = BO6		: REM VALVULA DE AGUA CALIENTE ON/OFF
    CCV4 = BO7		: REM VALVULA DE AGUA FRIA ON/OFF
    HCV4 = BO8		: REM VALVULA DE AGUA CALIENTE ON/OFF
    CCV5 = AO9		: REM VALVULA DE AGUA FRIA PROP
    HCV5 = AO10		: REM VALVULA DE AGUA CALIENTE PROP
    CCV6 = AO11		: REM VALVULA DE AGUA FRIA PROP
    HCV6 = AO12		: REM VALVULA DE AGUA CALIENTE PROP
    CCV7 = AO13		: REM VALVULA DE AGUA FRIA PROP
    HCV7 = AO14		: REM VALVULA DE AGUA CALIENTE PROP
    CCV8 = AO15		: REM VALVULA DE AGUA FRIA PROP
    HCV8 = AO16		: REM VALVULA DE AGUA CALIENTE PROP
    CCV9 = AO17		: REM VALVULA DE AGUA FRIA PROP
    HCV9 = AO18		: REM VALVULA DE AGUA CALIENTE PROP
    CCV10 = AO19	: REM VALVULA DE AGUA FRIA PROP
    HCV10 = AO20	: REM VALVULA DE AGUA CALIENTE PROP
    CCV11 = AV48	: REM VALVULA DE AGUA FRIA PROP
    HCV11 = AV49	: REM VALVULA DE AGUA CALIENTE PROP

REM ACTIVACION DE SISTEMAS DE ENFRIAMIENTO Y CALEFACCION

    REM *SISTEMA DE ENFRIAMIENTO
	    CCVB = MAX( CCV1 , CCV2 , CCV3 , CCV4 )
	    CCVA = MAX( CCV5 , CCV6 , CCV7 , CCV8 , CCV9 , CCV10 , CCV11 )

	    IF CCVA > 30 THEN CCVAB = 1
	    IF CCVA < 5 THEN CCVAB = 0

	    IF CCVB = 1 OR CCVAB = 1 THEN CV = 1 ELSE CV = 0

	    IF UMA1 = 1 AND TIMEON( CV ) > 90 THEN CCV = 1
	    IF UMA1 = 0 OR TIMEOFF( CV ) > 90 THEN CCV = 0

    REM *SISTEMA DE CALEFACCION
	    HCVB = MAX( HCV1 , HCV2 , HCV3 , HCV4 )
	    HCVA = MAX( HCV5 , HCV6 , HCV7 , HCV8 , HCV9 , HCV10 , HCV11 )

	    IF HCVA > 30 THEN HCVAB = 1
	    IF HCVA < 5 THEN HCVAB = 0

	    IF HCVB = 1 OR HCVAB = 1 THEN HV = 1 ELSE HV = 0
        
	    IF UMA1 = 1 AND TIMEON( HV ) > 90 THEN HCV = 1
	    IF UMA1 = 0 OR TIMEOFF( HV ) > 90 THEN HCV = 0

REM VARIABLES DE SALIDA

    BV20 = CCV
    BV21 = HCV

    REM *SINCRONIZACION DE DATOS CON CONTROLADOR DE PLANTAS DE AGUA
	    IF INTERVAL(00:00:10) THEN
	    	10002.BV36 = CCV
	    	10002.BV37 = HCV
	    ENDIF

END