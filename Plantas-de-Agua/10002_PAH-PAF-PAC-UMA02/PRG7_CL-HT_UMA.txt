REM PRG7_CL-HT_UMA (CONTROL: PAH PAF PAC UMA02)
REM OPERACION DE ENFRIAMIENTO, CALEFACCION Y DESHUMIDIFICACION DE UMA
REM AUTHOR CARLOS JIMENEZ HIRASHI @cjhirashi
REM VERSION 1.0.0

REM DECLARACION DE VARIABLES

    LOCALS ST_SF, SYS, OCC				: REM (LECTURA) ESTADO DE OPERACION DEL SISTEMA
    LOCALS SS_COOL, SS_HEAT				: REM (ESCRITURA) ACTIVACION DE SISTEMAS
    LOCALS CN_COOL, CN_HEAT			 	: REM (ESCRITURA) CONTROL DE SISTEMAS
    LOCALS COOL, HEAT : REM (VARIABLES INT) VARIABLE

REM VARIABLES DE ENTRADA

    SYS = BV38			: REM (BV) ACTIVACION DEL SISTEMA (0)INACTIVO (1)ACTIVO
    ST_SF = BI32		: REM (BV) ESTADO DE VENTIADOR DE SUMINISTRO (0)INACTIVO (1)ACTIVO

    IF SYS = 1 AND TIMEON( ST_SF ) > 30 THEN OCC = 1
    IF SYS = 0 OR ST_SF = 0 THEN OCC = 0

REM CONTROL DE TEMPERATURA

    REM *CONTROL PI SISTEMA 1
        LOCALS ENTRADA_1, SETPOINT_1, ERROR_1, SERROR_1
        LOCALS BP_C_1, RP_C_1, VI_C_1, RI_C_1, DEM_C_1
        LOCALS BP_H_1, RP_H_1, VI_H_1, RI_H_1, DEM_H_1

        REM **DECLARACION DE VARIABLES
        	ENTRADA_1 = AI27
        	SETPOINT_1 = AV18
        	BP_C_1 = 10
        	VI_C_1 = 0.5
        	BP_H_1 = 10
        	VI_H_1 = 0.5

        REM **ALGORITMO DE CONTROL
            REM ***DETECCION DE ERROR DEL SISTEMA
	            ERROR_1 = ENTRADA_1 - SETPOINT_1

            REM ***INCREMENTO DE ERROR EN EL SISTEMA
	            IF OCC = 1 THEN
	            	SERROR_1 = SERROR_1 + ( ERROR_1 / ( SCANS * 4 ))
	             ELSE
	            	SERROR_1 = 0
	            ENDIF
	            SERROR_1 = MAX((( 100 / VI_H_1 ) * -1 ) , MIN(( 100 / VI_C_1 ) , SERROR_1 ) )

            REM ***CALCULO DE DEMANDA DEL SISTEMA DE CONTROL
	            IF SERROR_1 > 0 THEN
	            	RP_C_1 = ( ERROR_1 / BP_C_1 ) * 100
	            	RP_H_1 = 0
	            	RI_C_1 = VI_C_1 * SERROR_1
	            	RI_H_1 = 0
	             ELSE
	            	RP_C_1 = 0
	            	RP_H_1 = (( ERROR_1 * -1 ) / BP_H_1 ) * 100
	            	RI_C_1 = 0
	            	RI_H_1 = VI_H_1 * ( SERROR_1 * -1 )
	            ENDIF

            REM ***ACTIVACION DE DEMANDA DEL SISTEMA
	            IF OCC = 1 THEN
	            	DEM_C_1 = MAX( 0 , MIN( 100 , ( RP_C_1 + RI_C_1 )))
	            	DEM_H_1 = MAX( 0 , MIN( 100 , ( RP_H_1 + RI_H_1 )))
	             ELSE
	            	DEM_C_1 = 0
	            	DEM_H_1 = 0
	            ENDIF

        REM **ASIGNACION DE DEMANDA
            CN_COOL = DEM_C_1
            CN_HEAT = DEM_H_1

    REM *ACTIVACION DE SISTEMAS DE ENFRIAMIENTO Y CALEFACCION*
    	IF CN_COOL > 30 THEN COOL = 1
    	IF CN_COOL < 5 THEN COOL = 0
    	IF CN_HEAT > 30 THEN HEAT = 1
    	IF CN_HEAT < 5 THEN HEAT = 0

    	IF SYS = 1 THEN
    		IF TIMEON( COOL ) > 30 THEN SS_COOL = 1
	    	IF TIMEOFF( COOL ) > 60 THEN SS_COOL = 0
	    	IF TIMEON( HEAT ) > 30 THEN SS_HEAT = 1
	    	IF TIMEOFF( HEAT ) > 60 THEN SS_HEAT = 0
	     ELSE
	    	IF SYS = 0 THEN SS_COOL = 0 , SS_HEAT = 0
	    ENDIF

    REM *SENAL DE ACTIVACION A CONTROL DE PLANTAS DE AGUA*
    	IF SYS = 1 AND SS_COOL = 1 THEN BV34 = 1 ELSE BV34 = 0
    	IF SYS = 1 AND SS_HEAT = 1 THEN BV35 = 1 ELSE BV35 = 0

REM VARIABLES DE SALIDA

    BV39 = SS_COOL		: REM ACTIVACION DE SISTEMA DE ENFRIAMIENTO
    BV40 = SS_HEAT 		: REM ACTIVACION DE SISTEMA DE CALEFACCION
    AO26 = CN_COOL		: REM CONTROL DE SISTEMA DE ENFRIAMIENTO
    AO27 = CN_HEAT 		: REM CONTROL DE SISTEMA DE CALEFACCION

END